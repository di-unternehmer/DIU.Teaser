prototype(DIU.Teaser:Component.Molecule.NewsTeaserSmall) < prototype(Neos.Fusion:Component) {
    #
    # Styleguide annotation
    # if this annotation is present (even when empty)
    # the prototype is rendered in the styleguide
    #
    @styleguide {

        #
        # The title of the component
        # Optional:  by default the component name is splitted and reversed
        #
        title = 'News Teaser Small'

        #
        # A description of the component
        # Optional: By default empty.
        #
        description = 'News Teaser Small'

        @propTypes {
            imageUrlDefault = ${PropTypes.string.isRequired}
            imageUrlLarge = ${PropTypes.string}
            imageUrlMedium = ${PropTypes.string}
            imageUrlSmall = ${PropTypes.string}

            description = ${PropTypes.string.isRequired}
            link = ${PropTypes.string.isRequired}
            title = ${PropTypes.string.isRequired}

            classNames = ${PropTypes.string}
        }

        #
        # Fusion-props to override during the styleguide rendering
        # Optional: By default empty.
        #
        props {
            imageUrlDefault = 'https://dummyimage.com/360x200/000/fff'
            imageUrlLarge = ''
            imageUrlMedium = ''
            imageUrlSmall = ''

            title = 'News'
            description = 'Wir halten Sie auf dem Laufenden: Klicken Sie sich hier durch unsere aktuellen Pressemitteilungen.'
            link = '#'

            classNames = ''
        }
    }

    imageUrlDefault = ''
    imageUrlLarge = ''
    imageUrlMedium = ''
    imageUrlSmall = ''

    title = ''
    description = ''
    link = ''

    classNames = ''

    #
    # The code afx`...` is converted to the fusion code below at parse time.
    # Attention: Currently there is no way to escape closing-backticks inside the Expression.
    #
    renderer = afx`
        <a class={'molecule-newsTeaserSmall col-md-4 ' + props.classNames} href={props.link}>
            <article vocab="http://schema.org/" typeof="Article" class="row">
                <DIU.Teaser:Component.Atom.Picture.Default @if.notEmpty={props.imageUrlDefault}
                    imageUrlSmall={props.imageUrlSmall}
                    SmallMaximumWidth="720"
                    imageUrlMedium={props.imageUrlMedium}
                    MediumMaximumWidth="940"
                    imageUrlLarge={props.imageUrlLarge}
                    LargeMaximumWidth="1140"
                    imageUrlDefault={props.imageUrlDefault}
                    DefaultMaximumWidth="1140"
                    classNames="article-image"
                />

                <div class="description" property="articleBody">
                    <header class="header">
                        <DIU.Teaser:Component.Atom.Headline.H2 @key="headline" classNames="headline" content={props.title} />
                    </header>
                    <p @if.has={props.description}>{props.description}</p>
                    <div class="pseudo-link">{Translation.translate('press.more',null,[],null,'DIU.Teaser')}</div>
                </div>
            </article>
       </a>
    `
}
