prototype(DIU.Teaser:Component.Molecule.NewsTeaser) < prototype(Neos.Fusion:Component) {
    #
    # Styleguide annotation
    # if this annotation is present (even when empty)
    # the prototype is rendered in the styleguide
    #
    @styleguide {

        #
        # The title of the component
        # Optional:  by default the component name is splitted and reversed
        #
        title = 'News Teaser'

        #
        # A description of the component
        # Optional: By default empty.
        #
        description = 'News Teaser'

        @propTypes {
            imageUrlDefault = ${PropTypes.string.isRequired}
            imageUrlLarge = ${PropTypes.string}
            imageUrlMedium = ${PropTypes.string}
            imageUrlSmall = ${PropTypes.string}

            publicationDate = ${PropTypes.string.isRequired}
            description = ${PropTypes.string.isRequired}
            title = ${PropTypes.string.isRequired}

            link = ${PropTypes.string.isRequired}
            altImageText = ${PropTypes.string}

            classNames = ${PropTypes.string}
        }

        #
        # Fusion-props to override during the styleguide rendering
        # Optional: By default empty.
        #
        props {
            imageUrlDefault = 'https://dummyimage.com/348x196/000/fff'
            imageUrlLarge = ''
            imageUrlMedium = ''
            imageUrlSmall = ''

            publicationDate = '18. Mai 2018'
            city = 'Hamburg'
            title = 'Tochterunternehmen von „Wer liefert was”:<br>EUROPAGES ernennt Julien Poilleux zum General Manager'
            description = 'Die europäische B2B-Plattform EUROPAGES (EP) hat zum 14. Mai 2018 ihr Management-Team um Julien Poilleux als neuen General Manager erweitert. Die Hauptaufgabe des 39-jährigen besteht darin, das Wachstum von EP weiter voranzutreiben.'

            link = '#'
            altImageText = ''

            classNames = ''
        }
    }

    imageUrlDefault = ''
    imageUrlLarge = ''
    imageUrlMedium = ''
    imageUrlSmall = ''

    publicationDate = ''
    city = ''
    title = ''
    description = ''

    link = ''
    altImageText = ''

    classNames = ''

    fallBackArticleImage = Neos.Fusion:ResourceUri {
        path = 'resource://Neos.Neos/Public/Images/dummy-image.png'
    }

    #
    # The code afx`...` is converted to the fusion code below at parse time.
    # Attention: Currently there is no way to escape closing-backticks inside the Expression.
    #
    renderer = afx`
        <a class={'molecule-newsTeaser ' + props.classNames} href={props.link}>
            <article vocab="http://schema.org/" typeof="Article" class="row">
                <div class="col-md-4">
                    <DIU.Teaser:Component.Atom.Picture.Default
                        imageUrlSmall={props.imageUrlSmall}
                        imageUrlMedium={props.imageUrlMedium}
                        imageUrlLarge={props.imageUrlLarge}
                        imageUrlDefault={props.imageUrlDefault ? props.imageUrlDefault : props.fallBackArticleImage}
                        classNames={"article-image" + (props.imageUrlDefault ? '' : ' news-fallback-svg')}
                        altText={props.altImageText}
                    />

                </div>
                <div class="body col-md-8">
                    <header class="header">
                        <DIU.Teaser:Component.Atom.PublishingInformation city={props.city} date={props.publicationDate}/>
                        <DIU.Teaser:Component.Atom.Headline.H2 @key="headline" classNames="headline" content={props.title} />
                    </header>

                    <div class="description" property="articleBody">
                        <p @if.has={props.description}>{props.description}</p>
                        <div class="pseudo-link">{Translation.translate('press.reading',null,[],null,'DIU.Teaser')}</div>
                    </div>
                </div>
            </article>
       </a>
    `
}
